import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "org.beryx.runtime" version "1.13.1"
}

group 'org.mrcube'
version = '24.5.1'

application {
    mainClass.set('org.mrcube.MR3')
}

project.ext {
    moduleName = 'org.mrcube.mrcube'
    artifactId = 'mrcube'
    defaultEncoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = '8.6'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.mrcube.MR3'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperty 'file.encoding', 'UTF-8'
}

tasks.withType(JavaExec) {
    systemProperty 'file.encoding', 'UTF-8'
}

test {
    useJUnitPlatform()
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'org.mrcube.MR3'
    }
}

runtime {
    enableCds()
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageDir = file("${layout.buildDirectory}/mrcube-${version}")
    imageZip = file("${layout.buildDirectory}/mrcube-${version}.zip")

    jpackage {
        if (OperatingSystem.current().isWindows()) {
            jpackageHome = 'C:\\Program Files\\Java\\jdk-21.0.3'
            imageOptions = ['--icon', './src/main/resources/images/mr3.ico']
            installerType = 'msi'
            installerOptions = ['--vendor', 'Takeshi Morita', '--win-shortcut']
        } else if (OperatingSystem.current().isMacOsX()) {
            jpackageHome = '/Library/Java/JavaVirtualMachines/jdk-21.jdk/Contents/Home'
            imageOptions = ['--verbose', '--icon', './src/main/resources/images/mr3.icns']
        }

        imageName = "mrcube-${version}"
        skipInstaller = false
        installerName = "mrcube"
    }
}

tasks.runtime.doLast {
    copy {
        from('./README.md')
        into("${layout.buildDirectory}/mrcube-${version}")
    }
    copy {
        from('./README.ja.md')
        into("${layout.buildDirectory}/mrcube-${version}")
    }
    copy {
        from('./LICENSE')
        into("${layout.buildDirectory}/mrcube-${version}")
    }
}

dependencies {
    implementation 'com.formdev:flatlaf:3.4.1'
    implementation 'jgraph:jgraph:5.13.0.0'
    implementation 'org.apache.jena:jena-core:5.0.0'
    implementation 'org.apache.jena:jena-arq:5.0.0'
    implementation 'org.slf4j:slf4j-simple:2.0.0'
    implementation 'org.apache.xmlgraphics:batik-svg-dom:1.17'
    implementation 'org.apache.xmlgraphics:batik-svggen:1.17'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
}

