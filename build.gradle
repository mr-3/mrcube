plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "org.beryx.runtime" version "1.12.7"
    id 'maven-publish'
    id 'idea'
}

group 'org.mrcube'
version = '22.3.1'

application {
    mainClass = 'org.mrcube.MR3'
}

project.ext {
    moduleName = 'org.mrcube.mrcube'
    artifactId = 'mrcube'
    defaultEncoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = '7.4.1'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.mrcube.MR3'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperty 'file.encoding', 'UTF-8'
}

tasks.withType(JavaExec) {
    systemProperty 'file.encoding', 'UTF-8'
}

idea {
    module {
        inheritOutputDirs = true
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

test {
    useJUnitPlatform()
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'org.mrcube.MR3'
    }
    archiveFileName = "mrcube-${archiveVersion}.jar"
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.desktop', 'java.logging']
    imageDir = file("$buildDir/mrcube-${version}")
    imageZip = file("$buildDir/mrcube-${version}.zip")

    jpackage {
        // macOS
        jpackageHome = '/Library/Java/JavaVirtualMachines/jdk-18.jdk/Contents/Home'
//        imageOptions = ['--verbose', '--icon', './src/main/resources/images/mr3.icns', '--mac-sign']
        imageOptions = ['--verbose', '--icon', './src/main/resources/images/mr3.icns']
        outputDir = 'my-packaging'
        mainJar = "mrcube-${version}.jar"
        imageName = "mrcube-${version}"
        skipInstaller = false
        installerName = "mrcube"
        // Windows
//        jpackageHome = 'C://jdk-18'
//        installerType = 'msi'
//        imageOptions = ['--icon', './src/main/resources/images/mr3.ico']
//        installerOptions = ['--vendor', 'Takeshi Morita', '--win-shortcut']
    }
}

tasks.runtime.doLast {
    copy {
        from('./README.md')
        into("$buildDir/mrcube-${version}")
    }
    copy {
        from('./README.ja.md')
        into("$buildDir/mrcube-${version}")
    }
    copy {
        from('./LICENSE')
        into("$buildDir/mrcube-${version}")
    }
}

dependencies {
    implementation 'com.formdev:flatlaf:2.0.2'
    implementation 'jgraph:jgraph:5.13.0.0'
    implementation 'org.apache.jena:jena-core:4.4.0'
    implementation 'org.apache.jena:jena-arq:4.4.0'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'org.apache.xmlgraphics:batik-svg-dom:1.14'
    implementation 'org.apache.xmlgraphics:batik-svggen:1.14'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId = 'org.mrcube'
            artifactId = 'mrcube'

            from components.java
            pom {
                name = 'MRCube'
                description = 'Meta-Model Management based on RDFs Revision Reflection'
                url = 'http://mrcube.org/'
                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE, Version 3'
                        url = 'http://www.gnu.org/licenses/gpl-3.0.html'
                    }
                }
                developers {
                    developer {
                        id = 't-morita'
                        name = 'Takeshi Morita'
                        email = 'morita@it.aoyama.ac.jp'
                    }
                }
            }
        }
    }
}

