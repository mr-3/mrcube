import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id "org.beryx.runtime" version "1.13.1"
}

java {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

group 'jp.ac.aoyama.it.ke.mrcube'
version = '25.3.1'

application {
    mainClass.set('jp.ac.aoyama.it.ke.mrcube.MR3')
}

project.ext {
    moduleName = 'jp.ac.aoyama.it.ke'
    artifactId = 'mrcube'
    defaultEncoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = '7.3'
}

jar {
    manifest {
        attributes 'Main-Class': 'jp.ac.aoyama.it.ke.mrcube.MR3'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperty 'file.encoding', 'UTF-8'
}

tasks.withType(JavaExec) {
    systemProperty 'file.encoding', 'UTF-8'
}

test {
    useJUnitPlatform()
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'jp.ac.aoyama.it.ke.mrcube.MR3'
    }
}

runtime {
    enableCds()
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageDir = layout.buildDirectory.dir("mrcube-${version}")
    imageZip = layout.buildDirectory.file("mrcube-${version}.zip")

    jpackage {
        if (OperatingSystem.current().isWindows()) {
            jpackageHome = 'C:\\Program Files\\Amazon Corretto\\jdk-21.0.6_7'
            installerType = 'msi'
            imageOptions = ['--verbose', '--icon', './src/main/resources/images/mr3.ico']
            installerOptions = ['--vendor', 'Takeshi Morita', '--win-shortcut']
        } else if (OperatingSystem.current().isMacOsX()) {
            jpackageHome = '/Library/Java/JavaVirtualMachines/amazon-corretto-21.jdk/Contents/Home'
            installerType = 'dmg'
            imageOptions = ['--verbose', '--icon', './src/main/resources/images/mr3.icns']
            installerOptions = ['--vendor', 'Takeshi Morita']
        }

        imageName = "mrcube-${version}"
        skipInstaller = false
        installerName = "mrcube"
    }
}

tasks.runtime.doLast {
    copy {
        from('./README.md')
        into(layout.buildDirectory.file("mrcube-${version}"))
    }
    copy {
        from('./README.ja.md')
        into(layout.buildDirectory.file("mrcube-${version}"))
    }
    copy {
        from('./LICENSE')
        into(layout.buildDirectory.file("mrcube-${version}"))
    }
}

dependencies {
    implementation 'com.formdev:flatlaf:3.5.4'
    implementation 'jgraph:jgraph:5.13.0.0'
    implementation 'org.apache.jena:jena-core:5.3.0'
    implementation 'org.apache.jena:jena-arq:5.3.0'
    implementation 'org.slf4j:slf4j-simple:2.0.17'
    implementation 'org.apache.xmlgraphics:batik-svg-dom:1.18'
    implementation 'org.apache.xmlgraphics:batik-svggen:1.18'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.12.0'
}

