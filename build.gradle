import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "org.beryx.runtime" version "1.12.7"
}

group 'org.mrcube'
version = '22.5.1'

application {
    mainClass.set('org.mrcube.MR3')
}

project.ext {
    moduleName = 'org.mrcube.mrcube'
    artifactId = 'mrcube'
    defaultEncoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

wrapper {
    gradleVersion = '7.4.2'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.mrcube.MR3'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperty 'file.encoding', 'UTF-8'
}

tasks.withType(JavaExec) {
    systemProperty 'file.encoding', 'UTF-8'
}

test {
    useJUnitPlatform()
}

shadowJar {
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': 'org.mrcube.MR3'
    }
    archiveFileName = "${project.name}-${project.version}.jar"
}


runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.desktop', 'java.logging']
    imageDir = file("$buildDir/mrcube-${version}")
    imageZip = file("$buildDir/mrcube-${version}.zip")

    jpackage {
        if (OperatingSystem.current().isWindows()) {
            jpackageHome = 'C:\\Program Files\\Java\\jdk-18.0.1.1'
            imageOptions = ['--icon', './src/main/resources/images/mr3.ico']
            installerType = 'msi'
            installerOptions = ['--vendor', 'Takeshi Morita', '--win-shortcut']
        } else if (OperatingSystem.current().isMacOsX()) {
            jpackageHome = '/Library/Java/JavaVirtualMachines/jdk-18.0.1.1.jdk/Contents/Home'
            imageOptions = ['--verbose', '--icon', './src/main/resources/images/mr3.icns']
        }

        outputDir = 'my-packaging'
        mainJar = "mrcube-${version}.jar"
        imageName = "mrcube-${version}"
        skipInstaller = false
        installerName = "mrcube"
    }
}

tasks.runtime.doLast {
    copy {
        from('./README.md')
        into("$buildDir/mrcube-${version}")
    }
    copy {
        from('./README.ja.md')
        into("$buildDir/mrcube-${version}")
    }
    copy {
        from('./LICENSE')
        into("$buildDir/mrcube-${version}")
    }
}

dependencies {
    implementation 'com.formdev:flatlaf:2.2'
    implementation 'jgraph:jgraph:5.13.0.0'
    implementation 'org.apache.jena:jena-core:4.4.0'
    implementation 'org.apache.jena:jena-arq:4.4.0'
    implementation 'org.slf4j:slf4j-simple:1.7.36'
    implementation 'org.apache.xmlgraphics:batik-svg-dom:1.14'
    implementation 'org.apache.xmlgraphics:batik-svggen:1.14'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

